import os

CC = 'clang'

CFLAGS = " ".join([
    '-Weverything',
    '-std=c11'
])

FILES = [
    'main.c',
    'lexer.c',
    'parser.c'
]

TEST_FILES = FILES[1:]
TEST_FILES.append('tests.c')

LEMON_FILES = ['lemon.c']

ragel = Builder(action = 'ragel -G2 $SOURCE -o $TARGET',
                suffix = '.c',
                src_suffix = '.rl')

lemon = Builder(action = 'build/lemon $SOURCE',
                suffix = '.c',
                src_suffix = '.y')

BUILDERS = {'Ragel' : ragel, 
            'Lemon' : lemon}

env = Environment( CC=CC
                  ,CCFLAGS=CFLAGS
                  ,BUILDERS=BUILDERS )

#Enable coloured output from Clang
env['ENV']['TERM'] = os.environ['TERM']

lemonp = env.Program('lemon', 'lemon.c')
parser = env.Lemon('parser.y')
env.Depends(parser, lemonp)

lex = env.Ragel('lexer.rl')

roth = env.Program('roth', FILES)

tests = env.Program('tests', TEST_FILES);
check = env.Command(target = 'check',
                    source = 'tests',
                    action = './' + tests[0].path)
env.Depends(check, tests)
